task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/lukasniemeier-zalando/log4j2-json-layout'

                scm {
                    connection 'scm:git:git@github.com:lukasniemeier-zalando/log4j2-json-layout.git'
                    developerConnection 'scm:git:git@github.com:lukasniemeier-zalando/log4j2-json-layout.git'
                    url 'https://github.com/lukasniemeier-zalando/log4j2-json-layout'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        name = 'Lukas Niemeier'
                        email = 'lukas.niemeier@zalando.de'
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}
